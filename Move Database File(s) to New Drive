USE master;
GO
DECLARE @name        SYSNAME
      , @dbname      SYSNAME
      , @filename    VARCHAR(128)
      , @oldlocation VARCHAR(MAX)
      , @sizemb      BIGINT
      , @command     VARCHAR(MAX)
      , @newdrive    VARCHAR(12)
      , @newlocation VARCHAR(MAX)
      , @errorlog    VARCHAR(1024);
SET @errorlog = CONVERT(VARCHAR(1024), SERVERPROPERTY('ErrorLogFileName'));
SET @errorlog = LEFT(@errorlog, LEN(@errorlog) - 8);
SET @newdrive = 'I:\';
SET @dbname = 'tempdb';
SET @filename = '%';
DECLARE db_cursor CURSOR
FOR SELECT name AS [LogicalName], 
           physical_name AS [Location], 
           CEILING(size / 128.0) SizeMB
    FROM sys.master_files
    WHERE database_id = DB_ID(@dbname)
          AND name LIKE @filename;
OPEN db_cursor;
FETCH NEXT FROM db_cursor INTO @name
                             , @oldlocation
                             , @sizemb;
WHILE @@fetch_status = 0
    BEGIN
        SET @command = 'ALTER DATABASE ' + QUOTENAME(@dbname) + ' MODIFY FILE (NAME = ' + @name + ', FILENAME = ''' + @oldlocation + ''');';
        SET @command = '/* ROLLBACK ' + 'FileName ' + @name + ', CurrentLocation: ' + @oldlocation + ', Current Size: ' + CONVERT(VARCHAR(14), @sizemb) + 'MB' + CHAR(10) + @command + CHAR(10) + '*/';
        PRINT @command;
        SET @newlocation = @newdrive + RIGHT(@oldlocation, LEN(@oldlocation) - 3);
        SET @command = '-- INSTALL ' + CHAR(10) + 'ALTER DATABASE ' + QUOTENAME(@dbname) + ' MODIFY FILE (NAME = ' + @name + ', FILENAME = ''' + @newlocation + ''');';
        PRINT @command;
        PRINT '/* ROBOCOPY ' + CHAR(10) + 'robocopy ' + LEFT(@oldlocation, LEN(@oldlocation) - CHARINDEX('\', REVERSE(@oldlocation), 1) + 1) + ' ' + LEFT(@newlocation, LEN(@newlocation) - CHARINDEX('\', REVERSE(@newlocation), 1) + 1) + ' ' + RIGHT(@oldlocation, CHARINDEX('\', REVERSE(@oldlocation)) - 1) + ' /S /r:3 /w:10 /log:' + @errorlog + @name + '_robocopy_' + CONVERT(VARCHAR(8), GETDATE(), 112) + '.txt /XF' + CHAR(10) + '*/';
        FETCH NEXT FROM db_cursor INTO @name
                                     , @oldlocation
                                     , @sizemb;
    END;
CLOSE db_cursor;
DEALLOCATE db_cursor;
IF @dbname = 'tempdb'
    PRINT '--Restart SQL this will recreate file on new drive';
    ELSE
    PRINT '/*' + CHAR(10) + ' ALTER DATABASE ' + @dbname + ' SET SINGLE_USER WITH ROLLBACK IMMEDIATE' + CHAR(10) + ' ALTER DATABASE ' + @dbname + ' SET MULTI_USER' + CHAR(10) + ' ALTER DATABASE ' + @dbname + ' SET OFFLINE' + CHAR(10) + '*/';
