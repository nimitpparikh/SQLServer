USE tempdb
GO

CREATE OR ALTER PROCEDURE #usp_CleanupWindowsLogins
(@Execute bit = 0)
AS
BEGIN
    SET NOCOUNT ON;
    DECLARE @name SYSNAME;
    DECLARE @SQL NVARCHAR(MAX);

    -- Create temporary table if it doesn't exist
    IF OBJECT_ID('tempdb..#tblValidateLogins') IS NULL
        CREATE TABLE [dbo].[#tblValidateLogins]
        (
            [SID]  VARBINARY(85) NULL,
            [name] NVARCHAR(256) NOT NULL
        );

    -- Insert validated logins into temporary table
    INSERT INTO #tblValidateLogins
    EXEC sp_validatelogins;

    -- Cursor to update database ownership
    DECLARE db_cursor CURSOR FOR
    SELECT name
    FROM sys.databases
    WHERE owner_sid IN (SELECT sid FROM #tblValidateLogins)
    AND state_desc = 'ONLINE'
    AND is_read_only = 0;

    OPEN db_cursor;
    FETCH NEXT FROM db_cursor INTO @name;

    WHILE @@FETCH_STATUS = 0
    BEGIN
        SET @SQL = 'ALTER AUTHORIZATION ON DATABASE ::[' + REPLACE(@name, ']', ']]') + '] TO [sa];';
        PRINT @SQL;
        IF @Execute = 1
            EXEC sp_executesql @SQL;
        FETCH NEXT FROM db_cursor INTO @name;
    END;

    CLOSE db_cursor;
    DEALLOCATE db_cursor;

    -- Cursor to update job ownership
    DECLARE agentjob_cursor CURSOR FOR
    SELECT name
    FROM msdb..sysjobs
    WHERE owner_sid IN (SELECT sid FROM #tblValidateLogins);

    OPEN agentjob_cursor;
    FETCH NEXT FROM agentjob_cursor INTO @name;

    WHILE @@FETCH_STATUS = 0
    BEGIN
        SET @SQL = 'EXEC msdb.dbo.sp_update_job @job_name=N''' + REPLACE(@name, '''', '''''') + ''', @owner_login_name=N''sa'';';
        PRINT @SQL;
        IF @Execute = 1
            EXEC sp_executesql @SQL;
        FETCH NEXT FROM agentjob_cursor INTO @name;
    END;

    CLOSE agentjob_cursor;
    DEALLOCATE agentjob_cursor;

    -- Cursor to drop users and logins
    DECLARE login_cursor CURSOR FOR
    SELECT name
    FROM #tblValidateLogins;

    OPEN login_cursor;
    FETCH NEXT FROM login_cursor INTO @name;

    WHILE @@FETCH_STATUS = 0
    BEGIN
        SET @SQL = 'EXEC sp_MSforeachdb ''USE [?]; IF EXISTS (SELECT * FROM [?].sys.database_principals WHERE name LIKE ''''' + REPLACE(@name, '''', '''''') + ''''') DROP USER ' + QUOTENAME(@name) + ';''';
        IF @Execute = 1
            EXEC sp_executesql @SQL;
        PRINT @SQL;

        SET @SQL = 'DROP LOGIN ' + QUOTENAME(@name);
        IF @Execute = 1
            EXEC sp_executesql @SQL;
        PRINT @SQL;

        FETCH NEXT FROM login_cursor INTO @name;
    END;

    CLOSE login_cursor;
    DEALLOCATE login_cursor;

    -- Drop temporary table
    DROP TABLE #tblValidateLogins;
END;
GO

-- Execute the stored procedure
EXEC #usp_CleanupWindowsLogins;
