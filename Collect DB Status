--Schdule this to run as SQL Agent job daily and at start of SQL Server Agent
--Below view can provide easy validation if any database count status changed from previous day. 
USE tempdb
GO
CREATE OR ALTER PROCEDURE usp_DBStatus
AS
IF NOT EXISTS (SELECT * FROM sys.objects WHERE object_id = OBJECT_ID(N'[dbo].[tblDBStatus]') AND type in (N'U'))
BEGIN
CREATE TABLE [dbo].[tblDBStatus](
	[name] [sysname] NOT NULL,
	[state_desc] [nvarchar](128) NULL,
	[RunTime] [datetime] NOT NULL
) ON [PRIMARY]
END
INSERT INTO tblDBStatus
SELECT 
    name, 
    CASE 
        WHEN is_read_only = 1 THEN state_desc + '; ReadOnly' 
        ELSE state_desc 
    END COLLATE SQL_Latin1_General_CP1_CI_AS AS CurrentStatus,
    GETDATE() AS CurrentRunTime
FROM 
    sys.databases
UNION
SELECT 'aaa-' + CAST(state_desc AS NVARCHAR(128)) COLLATE SQL_Latin1_General_CP1_CI_AS, CAST(COUNT(*) AS NVARCHAR(128)), GETDATE() AS RunTime
FROM sys.databases
GROUP BY state_desc;
WITH RankedRuns AS (
    SELECT *,
           ROW_NUMBER() OVER (partition by name ORDER BY RunTime DESC) AS RowNum
    FROM tblDBStatus
)
DELETE FROM tblDBStatus
WHERE RunTime < (SELECT max(RunTime) FROM RankedRuns WHERE  name = 'tempdb' and RowNum = 4);
GO
EXEC usp_DBStatus
GO
CREATE OR ALTER VIEW vwDBStatus
AS
WITH LastTwoEntries AS (
    SELECT 
        name,
        state_desc,
        RunTime,
        ROW_NUMBER() OVER (PARTITION BY name ORDER BY RunTime DESC) AS rn
    FROM 
        [dbo].[tblDBStatus]
)
, CurrentRun AS (
   SELECT 
    name, 
    CASE 
        WHEN is_read_only = 1 THEN state_desc + '; ReadOnly' 
        ELSE state_desc 
    END COLLATE SQL_Latin1_General_CP1_CI_AS AS CurrentStatus,
    GETDATE() AS CurrentRunTime
FROM 
    sys.databases
UNION
SELECT 'aaa-' + CAST(state_desc AS NVARCHAR(128)) COLLATE SQL_Latin1_General_CP1_CI_AS, CAST(COUNT(*) AS NVARCHAR(128)), GETDATE() AS RunTime
FROM sys.databases
GROUP BY state_desc
)
, PreviousRun AS (
    SELECT 
        name,
        state_desc AS PreviousStatus,
        RunTime AS PreviousRunTime
    FROM 
        LastTwoEntries
    WHERE RunTime = (SELECT RunTime FROM LastTwoEntries WHERE name = 'tempdb' AND rn = 1 )
)
, CombinedRuns AS (
    SELECT 
        COALESCE(c.name, p.name) AS name,
        c.CurrentStatus,
        p.PreviousStatus,
        c.CurrentRunTime,
        p.PreviousRunTime
    FROM 
        CurrentRun c
    FULL OUTER JOIN 
        PreviousRun p
    ON 
        c.name = p.name
)
SELECT 
    name,
    ISNULL(CurrentStatus, 'Dropped') AS CurrentStatus,
    ISNULL(PreviousStatus, 'New') AS PreviousStatus,
    ISNULL(CONVERT(VARCHAR(19),CurrentRunTime, 121), 'Dropped') AS CurrentRunTime,
    ISNULL(CONVERT(VARCHAR(19), PreviousRunTime,121), 'New') AS PreviousRunTime,
    CASE 
        WHEN CurrentStatus IS NULL THEN 'Dropped'
        WHEN PreviousStatus IS NULL THEN 'New'
        WHEN CurrentStatus <> PreviousStatus THEN 'Status Changed'
        ELSE 'No Change'
    END AS StatusChange
FROM 
    CombinedRuns
GO

select * from vwDBStatus
